"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("//diffutils/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//diffutils/private:versions.bzl", "TOOL_VERSIONS")

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for diffutils toolchain"
_ATTRS = {
    "diffutils_version": attr.string(mandatory = True, values = TOOL_VERSIONS.keys()),
    "platform": attr.string(mandatory = True, values = PLATFORMS.keys()),
}

def _diffutils_repo_impl(repository_ctx):
    url = "https://github.com/uutils/diffutils/releases/download/v{0}/diffutils-{1}.tar.xz".format(
        repository_ctx.attr.diffutils_version,
        repository_ctx.attr.platform,
    )
    repository_ctx.download_and_extract(
        url = url,
        strip_prefix = "diffutils-{}".format(repository_ctx.attr.platform),
        sha256 = TOOL_VERSIONS[repository_ctx.attr.diffutils_version][repository_ctx.attr.platform],
    )

    diff_binary = "diff"
    cmp_binary = "cmp"
    ext = ".exe" if repository_ctx.attr.platform.find("windows") >= 0 else ""

    # TODO: include sdiff, diff3 when they are available
    # https://github.com/uutils/diffutils/issues/16
    # https://github.com/uutils/diffutils/issues/15
    for b in [diff_binary, cmp_binary]:
        repository_ctx.symlink("diffutils%s" % ext, "%s%s" % (b, ext))

    build_content = """# Generated by diffutils/repositories.bzl
load("@diff.bzl//diffutils:toolchain.bzl", "diffutils_toolchain")

diffutils_toolchain(
    name = "diffutils_toolchain",
    diff_tool = "{diff}{ext}",
    cmp_tool = "{cmp}{ext}",
)
""".format(diff = diff_binary, cmp = cmp_binary, ext = ext)

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

diffutils_repositories = repository_rule(
    _diffutils_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def diffutils_register_toolchains(name, register = True, **kwargs):
    """Convenience macro for users which does typical setup.

    - create a repository for each built-in platform like "diff_linux_amd64"
    - TODO: create a convenience repository for the host platform like "diff_host"
    - create a repository exposing toolchains for each platform like "diff_platforms"
    - register a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.

    Args:
        name: base name for all created repos, like "diff1_14"
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each diff_repositories call
    """
    for platform in PLATFORMS.keys():
        diffutils_repositories(
            name = name + "_" + platform,
            platform = platform,
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
