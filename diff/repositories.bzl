"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("//diff/private:toolchains_repo.bzl", "PLATFORMS", "toolchains_repo")
load("//diff/private:versions.bzl", "TOOL_VERSIONS")

def _diffutils_repo_impl(rctx):
    BINS = ["cmp", "diff", "diff3", "sdiff"]
    ext = ".exe" if rctx.attr.platform.find("windows") >= 0 else ""

    for bin in BINS:
        rctx.download(
            url = "https://github.com/kormide/diffutils-prebuilt/releases/download/{version}/{bin}_{platform}{ext}".format(
                version = rctx.attr.diffutils_version,
                bin = bin,
                platform = rctx.attr.platform,
                ext = ext,
            ),
            executable = True,
            output = "{bin}{ext}".format(
                bin = bin,
                ext = ext,
            ),
            sha256 = TOOL_VERSIONS[rctx.attr.diffutils_version][bin][rctx.attr.platform],
        )

    build_content = """# Generated by @diff.bzl//diff/repositories.bzl
load("@diff.bzl//diff:toolchain.bzl", "diffutils_toolchain")

diffutils_toolchain(
    name = "diffutils_toolchain",
    cmp_bin = "cmp{ext}",
    diff_bin = "diff{ext}",
    diff3_bin = "diff3{ext}",
    sdiff_bin = "sdiff{ext}",
)
""".format(ext = ext)

    rctx.file("BUILD.bazel", build_content)

diffutils_repository = repository_rule(
    _diffutils_repo_impl,
    doc = "Fetch external tools needed for diffutils toolchain",
    attrs = {
        "diffutils_version": attr.string(mandatory = True, values = TOOL_VERSIONS.keys(), doc = "Version of diffutils binaries to download"),
        "platform": attr.string(mandatory = True, values = PLATFORMS.keys(), doc = "Platform to download binaries for"),
    },
)

# Wrapper macro around everything above, this is the primary API
def diffutils_register_toolchains(name, diffutils_version, register = True, **kwargs):
    """Convenience macro for users which does typical setup.

    - creates a repository for each built-in platform like "diff_linux_amd64"
    - creates a repository exposing toolchains for each platform in "diff_toolchains"
    - registers a toolchain pointing at each platform
    Users can avoid this macro and do these steps themselves, if they want more control.

    Args:
        name: base name for all created repos, like "diff1_14"
        diffutils_version: Version of diffutils prebuilt binaries to download
        register: whether to call through to native.register_toolchains.
            Should be True for WORKSPACE users, but false when used under bzlmod extension
        **kwargs: passed to each diff_repositories call
    """
    for platform in PLATFORMS.keys():
        diffutils_repository(
            name = name + "_" + platform,
            diffutils_version = diffutils_version,
            platform = platform,
            **kwargs
        )
        if register:
            native.register_toolchains("@%s_toolchains//:%s_toolchain" % (name, platform))

    toolchains_repo(
        name = name + "_toolchains",
        user_repository_name = name,
    )
